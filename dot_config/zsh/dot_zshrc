# Download zinit, if it's not here yet
ZINIT_HOME="${XDG_DATA_HOME}/zinit/zinit.git"
if [ ! -d $ZINIT_HOME ]; then 
    mkdir -p "$(dirname $ZINIT_HOME)"
    git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Source zinit
source "${ZINIT_HOME}/zinit.zsh"

# Better completion
setopt AUTO_LIST            # Automatically list choices on ambiguous completion.
setopt AUTO_PARAM_SLASH     # If completed parameter is a directory, add a trailing slash.

# Plugins from Oh-my-Zsh or Prezto
zinit lucid for \
    atinit"HISTFILE=${ZDOTDIR}/.zsh_history" \
  OMZL::history.zsh \

zinit wait lucid for \
  OMZL::completion.zsh \
  OMZL::directories.zsh \
  OMZL::git.zsh \
  OMZL::spectrum.zsh \
  OMZP::git \
    atload"zstyle ':omz:plugins:nvm' lazy yes" \
  OMZP::nvm

# Add some plugins
zinit light-mode wait lucid for \
    atinit"
      zstyle ':completion:*:default' list-prompt '%S%M matches%s'
      zstyle ':completion::complete:*' use-cache on
      zstyle ':completion::complete:*' cache-path "${XDG_CACHE_HOME}/zsh/zcompcache"
      zstyle ':completion:*:matches' group 'yes'
      zstyle ':completion:*:options' description 'yes'
      zstyle ':completion:*:descriptions' format ' %F{yellow}-- %d --%f'
      zstyle ':completion:*:warnings' format ' %F{red}-- no matches found --%f'
      zstyle ':completion:*' group-name ''
      zstyle ':completion:*' verbose yes
      zstyle ':completion:*' completer _complete _match _approximate
      zstyle ':completion:*:match:*' original only
      zstyle ':completion:*:approximate:*' max-errors 1 numeric" \
    blockf atpull'zinit creinstall -q .' \
  zsh-users/zsh-completions \
    atinit"zicompinit; zicdreplay" \
    atload"_zsh_autosuggest_start; bindkey '^[l' autosuggest-accept" \
  zsh-users/zsh-autosuggestions \
  zdharma-continuum/fast-syntax-highlighting \
    atinit"
      zstyle :history-search-multi-word page-size 10
      zstyle :history-search-multi-word highlight-color fg=red,bold
      zstyle :plugin:history-search-multi-word reset-prompt-protect 1" \
    bindmap"^R -> ^H" \
  zdharma-continuum/history-search-multi-word \
    atclone"dircolors -b LS_COLORS > clrs.zsh" \
    atload'zstyle ":completion:*:default" list-colors ${(s.:.)LS_COLORS}' \
    atpull'%atclone' pick"clrs.zsh" nocompile'!' \
  trapd00r/LS_COLORS

# keybindings
bindkey -v 
bindkey '^p' history-search-backward 
bindkey '^n' history-search-forward

# Load starship prompt
zinit ice as"command" from"gh-r" \
    atclone"./starship init zsh > init.zsh; ./starship completions zsh > _starship" \
    atpull"%atclone" src"init.zsh"
zinit light starship/starship

# zi cdreplay -q

# Development
(( $+commands[pyenv] )) && eval "$(pyenv init -)"

# Aliases
alias ls='ls --color'

# Shell integrations
# source <(fzf --zsh)

