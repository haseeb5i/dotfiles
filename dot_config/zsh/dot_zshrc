
ZINIT_HOME="${XDG_DATA_HOME}/zinit/zinit.git"
if [ ! -d $ZINIT_HOME ]; then 
    mkdir -p "$(dirname $ZINIT_HOME)"
    git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi
source "${ZINIT_HOME}/zinit.zsh"

_fix-omz-plugin() {
    [[ -f ./._zinit/teleid ]] || return 1
    local teleid="$(<./._zinit/teleid)"
    local pluginid
    for pluginid (${teleid#OMZ::plugins/} ${teleid#OMZP::}) {
        [[ $pluginid != $teleid ]] && break
    }
    (($?)) && return 1
    print "Fixing $teleid..."
    git clone --quiet --no-checkout --depth=1 --filter=tree:0 https://github.com/ohmyzsh/ohmyzsh
    cd ./ohmyzsh
    git sparse-checkout set --no-cone /plugins/$pluginid
    git checkout --quiet
    cd ..
    local file
    for file (./ohmyzsh/plugins/$pluginid/*~(.gitignore|*.plugin.zsh)(D)) {
        print "Copying ${file:t}..."
        cp -R $file ./${file:t}
    }
    rm -rf ./ohmyzsh
}

### begin configuration ###

# zsh/plugin settings
VI_MODE_SET_CURSOR=true
VI_MODE_RESET_PROMPT_ON_MODE_CHANGE=true
zstyle ":history-search-multi-word" page-size "11"

# https://github.com/ohmyzsh/ohmyzsh/tree/master/lib
zinit wait lucid for \
  OMZL::completion.zsh \
  OMZL::functions.zsh \
  OMZL::git.zsh \
  OMZL::history.zsh \
  OMZL::spectrum.zsh \
  OMZL::termsupport.zsh

# https://github.com/ohmyzsh/ohmyzsh/tree/master/plugins
zinit wait lucid for \
  OMZP::command-not-found \
  OMZP::nvm \
  OMZP::pyenv \
  OMZP::vi-mode
zinit wait lucid atpull"%atclone" atclone"_fix-omz-plugin" for \
  OMZ::plugins/gitfast 


zinit wait lucid light-mode for \
  Aloxaf/fzf-tab \
  atinit"zicompinit; zicdreplay" \
      zdharma-continuum/fast-syntax-highlighting \
  atload'_zsh_autosuggest_start; bindkey "^[l" autosuggest-accept' \
      zsh-users/zsh-autosuggestions \
  blockf atpull'zinit creinstall -q .' \
      zsh-users/zsh-completions \
    atclone"dircolors -b LS_COLORS > clrs.zsh" \
    atload'zstyle ":completion:*:default" list-colors ${(s.:.)LS_COLORS}' \
    atpull"%atclone" pick"clrs.zsh" nocompile'!' \
  trapd00r/LS_COLORS \
  wfxr/forgit \
  hlissner/zsh-autopair \
  urbainvaes/fzf-marks

zinit wait"1" lucid light-mode for \
  zdharma-continuum/history-search-multi-word \
  psprint/zsh-navigation-tools

zinit wait"1" lucid light-mode from"gh-r" as"command" for \
  junegunn/fzf \
    mv"bat* -> bat" pick"bat/bat" \
  @sharkdp/bat \
    mv"fd* -> fd" pick"fd/fd" \
  @sharkdp/fd \
  eza-community/eza \
    mv"direnv* -> direnv" \
    atclone'./direnv hook zsh > zhook.zsh' atpull'%atclone' \
    pick"direnv" src="zhook.zsh" \
  direnv/direnv

# starship prompt
zinit from"gh-r" as"command"  \
    atclone"./starship init zsh > init.zsh; ./starship completions zsh > _starship" \
    atpull"%atclone" src"init.zsh" for \
  starship/starship

# aliases

# keybindings
bindkey '^p' history-search-backward 
bindkey '^n' history-search-forward

